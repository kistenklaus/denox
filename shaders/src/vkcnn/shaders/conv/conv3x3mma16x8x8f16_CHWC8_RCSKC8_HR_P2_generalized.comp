#version 460

#extension GL_EXT_control_flow_attributes : require
#extension GL_KHR_memory_scope_semantics : require
#extension GL_EXT_shader_explicit_arithmetic_types_float16 : require
#extension GL_EXT_shader_explicit_arithmetic_types : require
#extension GL_EXT_shader_16bit_storage                   : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_KHR_shader_subgroup_basic                  : enable
#extension GL_KHR_cooperative_matrix : require

#pragma use_vulkan_memory_model

layout(constant_id = 0) const uint C = 8;
layout(constant_id = 1) const uint K = 8;

const uint SUBGROUP_SIZE = 32;
const uint WORKGROUP_SIZE = 256;

const uint S = 3; // kernel width
const uint R = 3; // kernel height

const uint TILE_W = 16;
const uint TILE_H = 8;

const uint PADDING_W = 1;
const uint PADDING_H = 1;

// cooperative matrix size NxKxM
const uint COOPMAT_N = 16;
const uint COOPMAT_K = 8;
const uint COOPMAT_M = 8;
// NOTE: This shader assumes that the cooperative matrix dimensions are a
// power of 2.

const uint IN_CHANNEL_BLOCK_SIZE = COOPMAT_K;
const uint VEC_SIZE = 8;

const uint FRAG_C_COUNT = K / COOPMAT_M;

layout(set = 0, binding = 0) readonly buffer input_tensor {
    uvec4 inputTensor[]; // CHW8/16 (f16)
};

layout(std430, set = 0, binding = 1, scalar) writeonly buffer output_tensor {
    uvec4 outputTensor[]; // CHW8/16 (f16)
};

layout(set = 0, binding = 2) readonly buffer weight_tensor {
    uvec4 weightTensor[]; // RCSKC8 (f16)
};

layout(push_constant) uniform PushConstant {
    uint Input_W;
    uint Input_H;
} pc;

const uint PIPELINE_DEPTH = 2;

// Static shared memory allocation (super ugly, but allows reuse of shared memory)
const uint SH_A_SIZE = ((COOPMAT_N + (S - 1)) * TILE_H * COOPMAT_K) / VEC_SIZE;
const uint SH_B_SIZE = (COOPMAT_K * K * S) / VEC_SIZE;
const uint SH_C_SIZE = COOPMAT_N * COOPMAT_M * TILE_H / VEC_SIZE;

const uint SH_PIPE_SIZE = PIPELINE_DEPTH * SH_A_SIZE + PIPELINE_DEPTH * SH_B_SIZE;
const uint SH_WB_SIZE = SH_C_SIZE;
const uint SH_SIZE = SH_PIPE_SIZE > SH_WB_SIZE ? SH_PIPE_SIZE : SH_WB_SIZE;
const uint SH_A_OFFSET = 0;
// const uint SH_B_OFFSET = PIPELINE_DEPTH * SH_A_SIZE;
const uint SH_B_OFFSET = 0;
const uint SH_C_OFFSET = 0;

shared uvec4 sh_A[PIPELINE_DEPTH * SH_A_SIZE];
shared uvec4 sh_B[PIPELINE_DEPTH * SH_B_SIZE];
shared uvec4 sh_C[SH_C_SIZE];

coopmat < float16_t, gl_ScopeSubgroup, COOPMAT_N, COOPMAT_M, gl_MatrixUseAccumulator > cFrag[FRAG_C_COUNT];

// wpixel: workgroup pixel top right (including padding)
// c : channel begin (will load COOPMAT_K) channels
// r : y kernel offset.
void loadA(in uint stage, in uvec2 wpixel, in uint c, in uint r) {
    const uint INVOC_PER_ROW = (COOPMAT_K / VEC_SIZE); // 1 or 2 basically.

    const uint local_c_blk = gl_SubgroupInvocationID & (INVOC_PER_ROW - 1);

    const uint c_blk = c / IN_CHANNEL_BLOCK_SIZE;

    const uint y = (wpixel.y - PADDING_H) + gl_SubgroupID + r;

    uint sharedOffset = (gl_SubgroupID * (COOPMAT_K * (COOPMAT_N + (S - 1)))) / VEC_SIZE;

    uint globalOffset = (c_blk * (pc.Input_H * pc.Input_W * IN_CHANNEL_BLOCK_SIZE) +
            y * (pc.Input_W * IN_CHANNEL_BLOCK_SIZE)) / VEC_SIZE;

    for (uint x = gl_SubgroupInvocationID / INVOC_PER_ROW;
        x < COOPMAT_N + (S - 1);
        x += SUBGROUP_SIZE / INVOC_PER_ROW) {
        uint o = x * INVOC_PER_ROW + local_c_blk;

        const bool inBounds = ((wpixel.x - PADDING_W + x) < pc.Input_W) && (y < pc.Input_H);

        uvec4 v;
        if (inBounds) {
            uint gIndex = globalOffset + INVOC_PER_ROW * (wpixel.x + x - PADDING_W) + local_c_blk;
            v = inputTensor[gIndex];
        } else {
            v = uvec4(0);
        }

        sh_A[SH_A_OFFSET + stage * SH_A_SIZE + sharedOffset + o] = v;
    }
}

void loadB(in uint stage, in uint r, in uint c) {
    const uint c_blk = c / VEC_SIZE;

    const uint offset = (r * (C * S * K) + c_blk * (S * K * VEC_SIZE)) / VEC_SIZE;

    const uint CBLK_PER_KMAT = COOPMAT_K / VEC_SIZE;

    const uint local_c_blk = gl_SubgroupInvocationID & (CBLK_PER_KMAT - 1);

    for (uint o = gl_LocalInvocationID.x / CBLK_PER_KMAT;
        o < (((S * COOPMAT_K) * K) / VEC_SIZE) / CBLK_PER_KMAT;
        o += WORKGROUP_SIZE / CBLK_PER_KMAT) {
        uint gIndex = o + local_c_blk * K * S;

        uvec4 v = weightTensor[offset + gIndex];

        uint sharedIdx = CBLK_PER_KMAT * o + local_c_blk;

        sh_B[SH_B_OFFSET + stage * SH_B_SIZE + sharedIdx] = v;
    }
}

void consumeGEMM(in uint stage) {
    uint aOffset = (gl_SubgroupID * ((COOPMAT_N + (S - 1)) * COOPMAT_K)) / VEC_SIZE;
    uint bOffset = 0;
    for (uint s = 0; s < S; ++s, aOffset += COOPMAT_K / VEC_SIZE, bOffset += COOPMAT_K * K / VEC_SIZE) {
        coopmat < float16_t, gl_ScopeSubgroup, COOPMAT_N, COOPMAT_K, gl_MatrixUseA > aFrag;

        coopMatLoad(aFrag, sh_A, SH_A_OFFSET + stage * SH_A_SIZE + aOffset, COOPMAT_K / VEC_SIZE,
            gl_CooperativeMatrixLayoutRowMajor);

        for (uint o = bOffset, n = 0; n < FRAG_C_COUNT; o += (COOPMAT_M * COOPMAT_K / VEC_SIZE), ++n) {
            coopmat < float16_t, gl_ScopeSubgroup, COOPMAT_K, COOPMAT_M, gl_MatrixUseB > bFrag;

            coopMatLoad(bFrag, sh_B, SH_B_OFFSET + stage * SH_B_SIZE + o, COOPMAT_K / VEC_SIZE,
                gl_CooperativeMatrixLayoutColumnMajor);

            cFrag[n] = coopMatMulAdd(aFrag, bFrag, cFrag[n]);
        }
    }
}

layout(local_size_x = WORKGROUP_SIZE, local_size_y = 1, local_size_z = 1) in;
void main(void) {
    // ================== SETUP ==================
    const uint wgPerRow = (pc.Input_W + TILE_W - 1) / TILE_W;
    const uvec2 wg2d = uvec2(gl_WorkGroupID.x % wgPerRow, gl_WorkGroupID.x / wgPerRow);
    const uvec2 wpixel = wg2d * uvec2(TILE_W, TILE_H);

    for (uint n = 0; n < FRAG_C_COUNT; ++n) {
        cFrag[n] = coopmat < float16_t, gl_ScopeSubgroup, COOPMAT_N, COOPMAT_M, gl_MatrixUseAccumulator > (float16_t(0));
    }

    const uint y = wpixel.y + gl_SubgroupID;

    uint stage = 0u;

    // ============= PIPE-PROLOG ==============
    {
        loadA(stage, wpixel, 0, 0);
    }

    {
        loadB(stage, 0, 0);
    }
    memoryBarrierShared();
    barrier();

    uint r = 1;
    for (uint c = 0; c < (C / COOPMAT_K); ++c) {
        for (; r < R; ++r) {
            // ============= PIPE-STAGE-LOADING ===============
            uint nextStage = stage ^ 1;
            if (y < pc.Input_H) {
                loadA(stage ^ 1, wpixel, c * COOPMAT_K, r);
            }
            {
                loadB(stage ^ 1, r, c * COOPMAT_K);
            }
            // ============ PIPE-STAGE-CONSUMING ==============

            if (y < pc.Input_H) {
                consumeGEMM(stage);
            }

            memoryBarrierShared();
            barrier();
            stage ^= 1;
        }
        r = 0;
    }

    if (y >= pc.Input_H) {
        return;
    }

    // ================= PIPE-EPILOG ==================
    consumeGEMM(stage);

    barrier();

    // ================= WRITE-BACK ===================
    bool boundaryTile = (wpixel.x + TILE_W) > pc.Input_W;
    if (boundaryTile) {
        uint offset = (wpixel.x * COOPMAT_M + (wpixel.y + gl_SubgroupID) * (pc.Input_W * COOPMAT_M)) / VEC_SIZE;
        uint stride = pc.Input_W * pc.Input_H * COOPMAT_M / VEC_SIZE;

        for (uint n = 0; n < FRAG_C_COUNT; ++n) {
            uint sharedOffet = gl_SubgroupID * COOPMAT_N * COOPMAT_M / VEC_SIZE;
            coopMatStore(cFrag[n], sh_C,
                sharedOffet,
                COOPMAT_M / VEC_SIZE,
                gl_CooperativeMatrixLayoutRowMajor);

            subgroupMemoryBarrierShared();
            subgroupBarrier();

            // CHWC16 (16x16x16)
            if (gl_SubgroupInvocationID < (COOPMAT_N * COOPMAT_M / VEC_SIZE)) {
                uvec4 v = sh_C[SH_C_OFFSET + sharedOffet + gl_SubgroupInvocationID];
                uint x = gl_SubgroupInvocationID / (COOPMAT_M / VEC_SIZE);
                if (wpixel.x + x < pc.Input_W) {
                    outputTensor[offset + gl_SubgroupInvocationID] = v;
                }
            }
            offset += stride;
        }
    } else {
        uint offset = (wpixel.x * COOPMAT_M + (wpixel.y + gl_SubgroupID) * pc.Input_W * COOPMAT_M) / VEC_SIZE;
        uint stride = (pc.Input_H * pc.Input_W * COOPMAT_M) / VEC_SIZE;
        for (uint n = 0; n < FRAG_C_COUNT; ++n) {
            coopMatStore(cFrag[n], outputTensor,
                offset,
                COOPMAT_M / VEC_SIZE,
                gl_CooperativeMatrixLayoutRowMajor);
            offset += stride;
        }
    }
}
